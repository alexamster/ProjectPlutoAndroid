apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'com.github.kt3k.coveralls'
apply from: '../jacoco.gradle'

coveralls {
    jacocoReportPath = "${buildDir}/reports/jacoco/testDebugUnitTestCoverage/testDebugUnitTestCoverage.xml"
}

android {
    compileSdkVersion 23
    // sdkmanager has problems downloading 23.0.2
    // https://github.com/JakeWharton/sdk-manager-plugin/issues/101
    //noinspection GradleDependency
    buildToolsVersion "23.0.1"

    defaultConfig {
        applicationId "com.projectpluto.projectplutoandroid"
        minSdkVersion 21
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        // bug in java 7 causes powermock to fail unless we use -noverify flag for tests.
        // https://github.com/jayway/powermock/issues/375
        jvmArgs "-noverify"
        testLogging {
            events 'passed', 'skipped', 'failed'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.squareup:otto:1.3.8'
    compile 'com.jakewharton.timber:timber:4.1.0'
    compile 'com.jakewharton:butterknife:7.0.1'

    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.0'
    testCompile 'org.mockito:mockito-core:1.10.19'
    testCompile ('org.powermock:powermock-api-mockito:1.6.4') {
        exclude module: 'hamcrest-core'
        exclude module: 'objenesis'
    }
    testCompile ('org.powermock:powermock-module-junit4:1.6.4') {
        exclude module: 'hamcrest-core'
        exclude module: 'objenesis'
    }
}
